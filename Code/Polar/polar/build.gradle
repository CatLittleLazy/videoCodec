plugins {
    id('com.android.library')
    id('kotlin-android')
    id('kotlin-kapt')
    id('com.google.protobuf')
    id("org.jetbrains.dokka") version "1.6.0"
    id('maven-publish')
}

android {
    namespace = 'com.polar.androidcommunications'

    compileSdkVersion 33

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 33
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            consumerProguardFiles 'lib-proguard-rules.pro'
        }
    }

    flavorDimensions 'library'
    productFlavors {
        androidCommunications {
            dimension "library"
        }
        sdk {
            dimension "library"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    publishing {
        singleVariant("sdkRelease") {
            withSourcesJar()
        }
    }
    packagingOptions {
        resources {
            excludes += ['**/*.proto']
        }
    }

}

tasks.dokkaJavadoc.configure {
    outputDirectory.set(file("docs/"))
    dokkaSourceSets {
        named("main") {
            perPackageOption {
                matchingRegex.set(".*\\.androidcommunications.*")
                suppress.set(true)
            }
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            aar(MavenPublication) {
                from components.sdkRelease

                groupId 'com.polar'
                artifactId project.getName()
            }
        }
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.20.0"
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
}

dependencies {
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.2'
    implementation 'io.reactivex.rxjava3:rxjava:3.1.6'
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'androidx.annotation:annotation:1.6.0'
    implementation "androidx.core:core-ktx:1.9.0"
    implementation "joda-time:joda-time:2.12.2"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.6.0"
    sdkImplementation 'com.google.protobuf:protobuf-javalite:3.20.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:5.2.0'
    testImplementation 'io.mockk:mockk:1.13.4'
    testImplementation 'androidx.test:runner:1.5.2'
    testImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:core:1.5.0'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'
}
